programa
{
	inclua biblioteca Graficos --> g
	inclua biblioteca Util --> u
//Frases e Variaveis GERAL

	const inteiro VidaMax = 7
	const cadeia Gato = "	      ▄▀▄     ▄▀▄\n	     ▄█  ▀▀▀▀▀  █▄\n	 ▄▄  █           █  ▄▄\n	█▄▄█ █  ▀  ┬  ▀  █ █▄▄█\n\n"
	const cadeia CmdFail = "Comando citado não existe, tente novamente\n"
	const cadeia Entrada = "Você entrou n", Foi = "Você foi para ", Faz = "\n\n\n Escolha algo para fazer\n"
	const cadeia Dano = "Você se machucou.", Conheceu = "Você conheceu "
	const cadeia FazMais = "Você quer fazer mais alguma coisa?\n\n"
	const cadeia Historia = "Cerveja, um gato perdido em uma cidade desconhecida, enfrenta solidão e perigos nas ruas.\nEle percorre ruas movimentadas, becos sombrios e terrenos acidentados, lutando contra a fome e a sede.\nSeu pelo está emaranhado e suas patas machucadas, mas ele persiste em sua busca por um lar.\nCerveja cruza com pessoas indiferentes que o ignoram, aumentando sua sensação de desamparo.\nNo entanto, em meio a sua angústia, Cerveja mantém a esperança\nde reencontrar sua família e sentir novamente o aconchego do lar.\n\n"
	const cadeia Creditos = "\n\nAs Aventuras Cerveja by Izabella e Sergio\n"
	
	inteiro Vida = 7
	inteiro Personagens = 0
	inteiro Sorteia
	inteiro VidaCachorro = 10
	inteiro Local = 12

	inteiro TempoAguardo = 1500
	inteiro TempoAguardoLongo = TempoAguardo * 5 
	
	funcao inicio()
	{
		//Inicio do codigo//
		Comeco()
		escreva(Gato, Historia)
		u.aguarde(TempoAguardoLongo)
		escreva("\n\nEscolha um local para ir:\n")
		const cadeia LocaisCidade = "\n[1] Casa\n[2] Praça\n[3] Mercado\n[0] Sair\nEscolha: "
		
		enquanto(Vida >= 1 e Local != 0 e VidaCachorro >= 1)
		{
			escreva(LocaisCidade)
			leia(Local)
			limpa()
			
			se(Local != 0)
			{
				
				enquanto(Local != 1 e Local != 2 e Local !=3)
				{
					escreva(CmdFail)
					escreva(LocaisCidade)
					leia(Local)
					limpa()
				}
			
				escolha(Local){
					
					caso 1:
						Casa()
					pare
					
					caso 2:
						Praca()
					pare
					
					caso 3:
						Mercado()
					pare
				}
		
			}//fim do se
			
		} //enquanto tem vida
	
		limpa()
		escreva("Personagens encontrados: ", Personagens, Creditos)
		u.aguarde(TempoAguardoLongo)
		gif()
	} //fim da funcao inicio()
	


		//Função Vida do Gato
	funcao VidaGato()
	{
		se(Vida > VidaMax)
		{
			Vida = 7
		}
		
		senao se(Vida == 7)
		{
			escreva("Vida: 		♡ ♡ ♡ ♡ ♡ ♡ ♡\n")
		}
		senao se(Vida == 6)
		{
			escreva("Vida: 		♡ ♡ ♡ ♡ ♡ ♡\n")
		}
		senao se(Vida == 5)
		{
			escreva("Vida:		♡ ♡ ♡ ♡ ♡\n")
		}
		senao se(Vida == 4)
		{
			escreva("Vida:		♡ ♡ ♡ ♡\n")
		}
		senao se(Vida == 3)
		{
			escreva("Vida:		♡ ♡ ♡\n")
		}
		senao se(Vida == 2)
		{
			escreva("Vida:		♡ ♡\n")
		}
		senao se(Vida ==1)
		{
			escreva("Vida:		♡\n")
		}
		senao se(Vida <= 0)
		{
			limpa()
			escreva("Suas vidas acabaram\n")
			escreva("Personagems encontrados: ", Personagens)
		}
				}

		//DENTRO DA CASA\\
		funcao Casa()
		{
			escreva("\n\n", Entrada, "a casa\n")
			escreva(Faz)
			const cadeia OqFazCasa = "[1] Ir para o quintal\n[2] Visitar quartos\n[3] Ver cozinha\n[0] Sair\n\nEscolha: "
			escreva(OqFazCasa)
			inteiro FazCasa
			leia(FazCasa)
			limpa()
			enquanto(FazCasa != 0)
			{
				enquanto(FazCasa != 1 e FazCasa != 2 e FazCasa != 3)
				{
					escreva(CmdFail)
					u.aguarde(TempoAguardo)
					escreva(OqFazCasa)
					leia(FazCasa)
					}
				se(FazCasa == 1)
				{
				Sorteia = u.sorteia(1, 2)
				 se(Sorteia == 1)
				 {
				 	escreva("Você foi atacado pelo cachorro.\n",Dano,"\n")
				 	Vida = Vida - 2
				 	VidaGato()
				 }se(Sorteia == 2)
				 {
				 	escreva(Conheceu, "Rufus, o cachorro")
				 	Personagens++
				 	u.aguarde(TempoAguardo)
				 }
					
				}se(FazCasa == 2)
				{
					Sorteia = u.sorteia(1, 2)
					se(Sorteia == 1)
					{
						escreva("O quaro esta vazio e você foi embora\n\n")
						u.aguarde(TempoAguardo)
					}se(Sorteia == 2)
					{
						escreva(Conheceu, "Jefferson, um humano\n\n")
						Personagens++
						u.aguarde(TempoAguardo)
					}
				}se(FazCasa == 3)
				{
					Sorteia = u.sorteia(1, 2)
					se(Sorteia == 1)
					{
						escreva("Você bebeu um pouco de água e saiu\n\n")
						Vida = Vida + 1
						VidaGato()
						u.aguarde(TempoAguardo)
					}se(Sorteia == 2)
					{
						escreva(Conheceu, "Luciano, un humanito.\n\n")
						Personagens++
						u.aguarde(TempoAguardo)
					}
				}
				escreva(FazMais)
				escreva(OqFazCasa)
				leia(FazCasa)
						}
				}
		
		
		//PAÇA DA CIDADE\\			
		funcao Praca()
		{
			escreva("\n\n", Foi, "a praça\n")
				escreva(Gato)
				escreva(Faz)
				const cadeia OqFazPraca = "[1] Brincar na areia\n[2] Brincar no parquinho\n[3] Ir para a floresta (Essa entrada não tem volta!) \n0:Sair\n\nEscolha: "
				escreva(OqFazPraca)
				inteiro FazPraca
				leia(FazPraca)
				limpa()
				enquanto(FazPraca != 0)
				{
					enquanto(FazPraca != 1 e FazPraca != 2 e FazPraca != 3)
					{
						escreva(CmdFail)
						u.aguarde(TempoAguardo)
						leia(FazPraca)
						limpa()
					}
					se(FazPraca == 1)
					{
						Sorteia = u.sorteia(1, 2)
						se(Sorteia == 1)
						{
							escreva("Tinha vidro na areia. ",Dano)
							Vida = Vida - 1
							VidaGato()
							u.aguarde(TempoAguardo)
						}se(Sorteia == 2)
						{	
							escreva(Conheceu, "Pablo Vegetti, a criança")
							Personagens++
							u.aguarde(TempoAguardo)
						}
						}
					se(FazPraca == 2)
					{
						Sorteia = u.sorteia(1, 2)
						
						se(Sorteia == 1)
						{
							escreva("Você estava brincando quando caiu do escorregador. ")
							escreva(Dano)
							Vida = Vida - 1
							VidaGato()
							u.aguarde(TempoAguardo)
						}se(Sorteia == 2)
						{
							escreva("Você estava brincando quando você avista alguem.", Conheceu, "Furico, o gato.")
							Personagens++
							u.aguarde(TempoAguardo)
						}
					}
					se(FazPraca == 3)
					{
						Batalha()
						pare
					}
				escreva(FazMais)
				escreva(OqFazPraca)
				leia(FazPraca)
				}
				}

		//MERCADINHO\\
		funcao Mercado()
		{
			escreva("\n\n", Entrada, "o mercado do seu Luiz\n")
			escreva(Faz)
			const cadeia OqFazMercado = "[1] Ir para a padaria\n[2] Ir para o açougue\n[3] Ir para o setor de bebidas\n[0] Sair\n\nEscolha: "
			const cadeia Comer = "Você comeu um"
			escreva(OqFazMercado)
			inteiro FazMercado
			leia(FazMercado)
			limpa()
			enquanto(FazMercado != 0)
			{
				enquanto(FazMercado != 1 e FazMercado != 2 e FazMercado != 3)
				{
					escreva(CmdFail)
					u.aguarde(TempoAguardo)
					escreva(OqFazMercado)
					leia(FazMercado)
				}
				se(FazMercado == 1)
				{
				Sorteia = u.sorteia(1, 2)
				 se(Sorteia == 1)
				 {
				 	escreva(Comer," pão velho.\n\n")
				 	u.aguarde(TempoAguardo)
				 }se(Sorteia == 2)
				 {
				 	escreva(Comer," pão delicinha\n\n")
				 	Vida++
				 	u.aguarde(TempoAguardo)
				 }
					
				}se(FazMercado == 2)
				{
					Sorteia = u.sorteia(1, 2)
					se(Sorteia == 1)
					{
						escreva(Comer,"a carne estranha\n\n")
						u.aguarde(TempoAguardo)
					}se(Sorteia == 2)
					{
						escreva(Comer, "a picanha perfeita\n\n")
						Vida++
						u.aguarde(TempoAguardo)
					}
				}se(FazMercado == 3)
				{
					Sorteia = u.sorteia(1, 2)
					se(Sorteia == 1)
					{
						escreva("Você bebeu um pouco de água e saiu\n\n")
						Vida++
						VidaGato()
						u.aguarde(TempoAguardo)
					}se(Sorteia == 2)
					{
						escreva("Você bebeu uma Coca geladinha.\n\n")
						Vida++
						u.aguarde(TempoAguardo)
					}
				}
				escreva(FazMais)
				escreva(OqFazMercado)
				leia(FazMercado)
						}
					}
			//BATALHA EPICA\\
		funcao Batalha()
		{
			cadeia Cao = " ╭━━╮╭━━╮\n ┃╭╮┗┻━━┻━╮\n ┃╰┓╭━╮╭━╮┃\n ╰━┓┃▇┃┃▇┃┃\n   ┃╱▔▔▔▔▔▔▔▇\n   ┃▏┏┳┳┳┳┳━┛\n   ┃╲╰┻┻┻┻┻┓\n\n"
			escreva(Cao, "Esse é Clark, o cachorro novo de sua antiga familia.\nEle não gosta de gatos.")
			u.aguarde(TempoAguardoLongo)
			enquanto(VidaCachorro > 0 e Vida > 0){
				inteiro Opcao
				inteiro Chance = u.sorteia(1,2)
				escreva("\n\nEscolha um ataque:\n[1] Arranhar	[2] Morder\n\n")
				leia(Opcao)

				se(Opcao == Chance e Opcao == 1)
				{
					escreva("Você arranhou Clark.\n")
					VidaCachorro = VidaCachorro - 1
					VidaCao()
					u.aguarde(TempoAguardo)
					limpa()
				}
				se(Opcao != Chance e Opcao == 1)
				{
					escreva("Clark te arranhou.\n")
					Vida = Vida - 1
					VidaGato()
					u.aguarde(TempoAguardo)
					limpa()
				}
				se(Opcao == Chance e Opcao == 2)
				{
					escreva("Você mordeu Clark.\n")
					VidaCachorro = VidaCachorro - 2
					VidaCao()
					u.aguarde(TempoAguardo)
					limpa()
				}
				se(Opcao != Chance e Opcao == 2)
				{
					escreva("Clark te mordeu.\n")
					Vida = Vida - 2
					VidaGato()
					u.aguarde(TempoAguardo)
					limpa()
			}
			}
					}
		//VIDA DO CACHORRO\\
		funcao VidaCao()
		{
			se(VidaCachorro == 10){
				escreva("Vida Clark: ▇▇▇▇▇▇▇▇▇▇10▇▇▇▇▇▇▇▇▇▇")
			}
			senao se(VidaCachorro == 9)
			{
				escreva("Vida Clark: ▇▇▇▇▇▇▇▇▇9▇▇▇▇▇▇▇▇▇ ")
			}
			senao se(VidaCachorro == 8)
			{
				escreva("Vida Clark: ▇▇▇▇▇▇▇▇8▇▇▇▇▇▇▇▇ ")
			}
			senao se(VidaCachorro == 7)
			{
				escreva("Vida Clark: ▇▇▇▇▇▇▇7▇▇▇▇▇▇▇ ")
			}
			senao se(VidaCachorro == 6)
			{
				escreva("Vida Clark: ▇▇▇▇▇▇6▇▇▇▇▇▇ ")
			}
			senao se(VidaCachorro == 5)
			{
				escreva("Vida Clark: ▇▇▇▇▇5▇▇▇▇▇ ")
			}
			senao se(VidaCachorro == 4)
			{
				escreva("Vida Clark: ▇▇▇▇4▇▇▇▇ ")
			}
			
			senao se(VidaCachorro == 3)
			{
				escreva("Vida Clark: ▇▇▇3▇▇▇ ")
			}
			
			senao se(VidaCachorro == 2)
			{
				escreva("Vida Clark: ▇▇2▇▇ ")
			}
			
			senao se(VidaCachorro == 1)
			{
				escreva("Vida Clark: ▇1▇ ")
			}
			senao se(VidaCachorro <= 0)
			{
				escreva("VOCÊ VENCEU\n\n		FIM	\nPersonagens encontrados: ", Personagens, Creditos)
			}
					}
		//TELA INICIAL\\
		funcao Comeco()
		{
			const cadeia Titulo = " _______      _______         __              ______                          __ \n|       |    |     __|.---.-.|  |_.-----.    |      |.-----.----.--.--.-----.|__|.---.-.\n|   -   |    |    |  ||  _  ||   _|  _  |    |   ---||  -__|   _|  |  |  -__||  ||  _  |\n|_______|    |_______||___._||____|_____|    |______||_____|__|  \\___/|_____||  ||___._|\n                                                                            |___|       \n\n\n[1][JOGAR]\n\n[2][Creditos]\n\n\nEscolha: "
			inteiro Escolher

			escreva(Titulo)
			leia(Escolher)
			limpa()
			enquanto(Escolher!=1 e Escolher!=2)
			{
				escreva(Titulo)
				escreva(CmdFail)
				leia(Escolher)
				limpa()
			}
			enquanto(Escolher == 2)
			{
				escreva(Creditos)
				u.aguarde(TempoAguardoLongo)
				limpa()
				escreva(Titulo)
				leia(Escolher)
				limpa()
			}
		}

		funcao gif()
		{
			
			cadeia i
			logico a
			inteiro cat = g.carregar_imagem("/home/lab/Área de Trabalho/gate.gif")
				
			se(Personagens == 5)
			{

				escreva("Como você conheceu todos os personagens, aquiestá um presente..\n")
				a = verdadeiro 
				g.iniciar_modo_grafico(a)
				g.definir_dimensoes_janela(909, 909)
	
				enquanto(verdadeiro)
				{
				
					g.desenhar_imagem(0, 0, cat)
					g.renderizar()
					
			
				}
			}
			senao
			{
				
				escreva("\nVocê perdeu um segredinho.. :(")
			}
		
			
		}
	}
